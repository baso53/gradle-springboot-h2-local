plugins {
	id 'org.springframework.boot' version '2.5.7'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
}

group = 'gradle.springboot.h2.local'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

configurations {
	localH2Implementation.extendsFrom implementation
	localH2RuntimeOnly.extendsFrom runtimeOnly
}

sourceSets {
	localH2 {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	runtimeOnly 'org.postgresql:postgresql'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	localH2RuntimeOnly 'com.h2database:h2'
}

configurations.localH2Implementation {
	exclude group: 'org.postgresql', module: 'postgresql'
}

test {
	useJUnitPlatform()
}
